plugins {
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation group: 'net.devh', name: 'grpc-server-spring-boot-starter', version: '3.1.0.RELEASE'
    implementation 'io.grpc:grpc-protobuf:1.69.0'
    implementation 'io.grpc:grpc-stub:1.69.0'
    implementation "io.grpc:grpc-netty-shaded:1.69.0"
    implementation 'com.google.protobuf:protobuf-java:4.29.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.29.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.69.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'annotate_for_java_generated=false'
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/generated/main/java'
            srcDirs 'src/generated/main/grpc'
        }
        proto {
            srcDirs 'src/main/proto'
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

test {
    useJUnitPlatform()
}

tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}