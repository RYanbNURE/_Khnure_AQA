plugins {
    id 'java'
    id 'application'
    id 'org.openapi.generator' version '6.6.0'
}

group 'com.example'
version '1.0-SNAPSHOT'

sourceCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.11'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.threeten:threetenbp:1.6.8'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$projectDir/src/main/resources/api.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.example.api"
    modelPackage = "com.example.model"
    configOptions = [
            dateLibrary: "java8"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += [
                    "${buildDir}/generated/src/main/java"
            ]
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate

application {
    mainClass = 'com.example.Main'
}

test {
    useJUnitPlatform()
}